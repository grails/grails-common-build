apply plugin:'groovy'
apply plugin: 'java-library'

sourceCompatibility=1.17
targetCompatibility=1.17

repositories {
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    documentation.extendsFrom compileClasspath
}

configurations {
    all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details -> 
            String group = details.requested.group
            if (details.requested.group == "org.codehaus.groovy") {
                details.useTarget(group: "org.apache.groovy", name: details.requested.name, version: details.requested.version)
                details.because "The dependency coordinates are changed in Apache Groovy 4"
            }
        }
    }
}

dependencies {
    api platform("org.apache.groovy:groovy-bom:$groovyVersion")
    api "org.apache.groovy:groovy:$groovyVersion"
    api "org.slf4j:slf4j-api:$slf4jVersion"

    testImplementation "org.spockframework:spock-core:${spockVersion}", {
        exclude module:'groovy-all'
    }
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation "org.objenesis:objenesis:1.4"

    documentation "org.fusesource.jansi:jansi:1.14"

}

tasks.named("compileTestGroovy") {
    options.forkOptions.jvmArgs += ["-Dspock.iKnowWhatImDoing.disableGroovyVersionCheck=true"]
}

tasks.withType(Test).configureEach {
    systemProperty 'spock.iKnowWhatImDoing.disableGroovyVersionCheck', 'true'
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    if (isCiBuild) {
        maxParallelForks = 2
    } else {
        maxParallelForks = 4
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

groovydoc.classpath += configurations.documentation
